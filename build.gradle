plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'cn.apisium.nekomaid'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    targetCompatibility = JavaVersion.toVersion(javaVersion)
}

repositories {
    mavenCentral()

    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://www.jitpack.io'
    }
    maven {
        name = 'PlugMan'
        url = 'https://raw.githubusercontent.com/TheBlackEntity/PlugMan/repository/'
    }
    /*maven {
        name = 'ServerUtils'
        url = 'https://repo.fvdh.dev/releases'
    }*/
    maven {
        name = 'placeholderapi'
        url = 'https://repo.helpch.at/releases'
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.io/repository/maven-public/'
    }
    maven {
        name = 'multiverseMultiverseReleases'
        url = 'https://repo.onarandombox.com/multiverse-releases'
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:${paperApi}"
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'io.netty:netty-all:4.2.5.Final'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.25.3'
    compileOnly "com.github.Apisium:Uniporter:${uniporter}"
    compileOnly "com.github.jikoo.OpenInv:openinvapi:${openInv}"
    compileOnly "me.clip:placeholderapi:${placeholderApi}"
    compileOnly "de.tr7zw:item-nbt-api-plugin:${itemNbtApi}"
    compileOnly "com.rylinaux:PlugMan:${plugman}"
    //compileOnly("net.frankheijden.serverutils:ServerUtils:3.5.3") SSL炸了不知道多久了
    compileOnly 'com.github.oshi:oshi-core:6.3.1'
    compileOnly "org.mvplugins.multiverse.core:multiverse-core:${multiverseCore}"
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly ('org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT') {
        exclude group: 'org.bukkit'
    }

    implementation 'org.json:json:20231013'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.19'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'com.github.Apisium:netty-engine.io:fdd2609085'
    implementation 'org.apache.commons:commons-compress:1.26.0'
    implementation 'commons-io:commons-io:2.14.0'
    implementation ('io.socket:engine.io-server:6.2.1') {
        exclude group: 'com.google.code.gson'
    }
    implementation ('com.maxmind.geoip2:geoip2:2.15.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'commons-codec'
    }
    implementation ('io.socket:socket.io-server:4.0.1') {
        exclude group: 'org.json'
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'io.socket', module: 'engine.io-server'
        exclude group: 'io.socket', module: 'engine.io-client'
    }
    implementation ('io.netty:netty-codec-http:4.2.8.Final') {
        exclude group: 'io.netty'
    }

    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
}

shadowJar {
    relocate('com.alibaba', 'cn.apisium.nekomaid.libs.com.alibaba')
    relocate('org.json', 'cn.apisium.nekomaid.libs.org.json')
    relocate('javax.servlet', 'cn.apisium.nekomaid.libs.javax.servlet')
    relocate('org.bstats', 'cn.apisium.nekomaid.libs.org.bstats')
    relocate('org.apache.commons.io', 'cn.apisium.nekomaid.libs.org.apache.commons.io')
    relocate('org.apache.commons.compress', 'cn.apisium.nekomaid.libs.org.apache.commons.compress')
    relocate('io.socket', 'cn.apisium.nekomaid.libs.io.socket')
    relocate('com.maxmind', 'cn.apisium.nekomaid.libs.com.maxmind')
    relocate('io.netty.handler.codec.http.websocketx', 'cn.apisium.nekomaid.libs.io.netty.handler.codec.http.websocketx')
    relocate('io.netty.handler.codec.http.multipart', 'cn.apisium.nekomaid.libs.io.netty.handler.codec.http.multipart')
    exclude 'io/papermc/**'
    exclude 'com/janboerman/**'
    exclude 'io/netty/handler/codec/http/*'

    archiveClassifier.set('')
    minimize()
}
build.dependsOn(shadowJar)
